apply plugin: 'com.android.application'

ext {
    versionMajor = 1
    versionMinor = 9
    versionPatch = 29
}

android {
    signingConfigs {
        config {
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file('keystore.jks')
            storePassword System.getenv("STORE_PASSWORD")
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.renanbolonha.projetocircleci"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        beta {
            versionNameSuffix 'b'
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            //versionNameSuffix 'b'
        }
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return (ext.versionMajor * 100000) + (ext.versionMinor * 1000) + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}." + generateBuilderNumber()
}

private String generateBuilderNumber() {
    String buildNumber = System.getenv('CIRCLE_BUILD_NUM')
    int versionNumber = 0
    if(buildNumber != null) {
        versionNumber = Integer.parseInt(buildNumber)
    }
    return versionNumber
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
